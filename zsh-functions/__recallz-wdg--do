#!/bin/zsh -f
emulate -L zsh
if whence __recallz-load >/dev/null; then
else
  builtin autoload -Uz __recallz-load && __recallz-load
fi

function (){
  if [[ "$LBUFFER" =~ '^ *$' ]]; then
    return
  fi
  local out
  out="$(__recallz-deno "do" "$LBUFFER")"
  if [[ $? -ne 0 ]]; then
    echo
    zle reset-prompt
    return
  elif [[ -z "$out" ]]; then
    zle reset-prompt
    return
  fi

  local outary=("${(f)out}")
  local argv="${(F)${(@)outary[1,-2]}}"
  local ftail="${outary[-1]}"
  local ext file
  for ext in zsh sh; do
    if [[ -f "$ftail.$ext" ]]; then
      file="$ftail.$ext"
      break
    fi
  done
  if [[ -z "$file" ]]; then
    __recallz-print "fail in loading kind file: \`$ftail.{zsh,sh}\`"
  fi
  source "$file"
  zle reset-prompt
}
